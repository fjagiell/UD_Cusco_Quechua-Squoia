rule mergens_vf {
  pattern {
    V [xpos=Root];
    NS [upos=NS, VerbForm];
    V -[ns]-> NS;
  }

  commands {
    V.form = V.form + NS.form;
    V.VerbForm = NS.VerbForm;

    shift NS ==> V;
    del_node NS;
  }
}

rule mergens_as {
  pattern {
    V [xpos=Root];
    NS [upos=NS, Aspect];
    V -[ns]-> NS;
  }

  commands {
    V.form = V.form + NS.form;
    V.Aspect = NS.Aspect;

    shift NS ==> V;
    del_node NS;
  }
}

 
rule cas { 
  pattern {
    Root [xpos=Root];
    C [upos=Cas];
    C -[s.arg|poss.subj]-> Root;
  }
  commands{
    Root.form = Root.form + C.form;
    Root.Case = C.Case;
    
    shift C ==> Root;
    del_node C;
  }
}

rule case_cleanup {
  pattern {
    Root [xpos=Root];
    C [upos=Cas];
    Root -[s.co|s.arg]-> C;
  }
  commands{
    C.upos = ADP;
  }
}

rule amb{
  pattern{
    A [upos=Amb];
    Root [xpos=Root];
    Root -[ev|topic]-> A;
  }
  commands{
    A.upos = PART;
    del_edge Root -[ev]-> A;
    add_edge Root -[discourse]-> A;
  }
}

rule suffix {
  pattern { 
    Root [xpos=Root];
    Suffix [xpos=SUFFIX];
    Root -[s.poss.subj|poss.subj|s.subj]-> Suffix;
  }
  commands{
    Root.form = Root.form + Suffix.form;

    shift Suffix ==> Root;
    del_node Suffix;
  }
}

rule nsubj{
  pattern{
    N [upos=NOUN];
    Root [upos=VERB];
    Root -[poss.subj]-> N;
  }
  commands{
    del_edge Root -[poss.subj]-> N;
    add_edge Root -[nsubj]-> N;
  }
}


rule csubj{
  pattern{
    V [xpos=Root,upos=VERB];
    Root [upos=VERB];
    V -[poss.subj]-> Root;
  }
  commands{
    del_edge V -[poss.subj]-> Root;
    add_edge V -[csubj]-> Root;
  }
}


rule vderiv {
  pattern{
    Root [xpos=Root];
    Vderiv [xpos=VDeriv];
    Root -> Vderiv;
  }
  commands{
    Root.form = Root.form + Vderiv.form;
    shift Vderiv ==> Root;
    del_node Vderiv;
  }
}

rule pred{
  pattern{
    Root [upos=VERB];
    O [upos=VERB];
    Root -[pred]-> O;
  }
  commands{
    del_edge Root -[pred]-> O;
    add_edge Root -[dep]-> O;
  }
}



strat main { Seq (Onf(mergens_vf), Onf(mergens_as), Onf(suffix), Onf(cas), Onf(case_cleanup), Onf(vderiv), Onf(amb), Onf(csubj), Onf(nsubj), Onf(pred)) }


